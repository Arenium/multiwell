c
c      Interpolation to obtain partition function and other functions 
c      at a temperature TT. Interpolation arrays QT, Cx, Hx, and Sx
c      possess elements at temperatures Tx(LL), read from a file 
c      generated by ADENSUM. Index i is the species.
c
      SUBROUTINE qvbint( i , TT , Qxx , Cxx , Hxx , Sxx )

      include 'declare.inc'
      
      INTEGER kx , jx
      REAL(8) xa , xb , xc , xx , diff , slope , delta
      SAVE
      
      IF( TT.GE.(Tx(1)-0.01) .AND. TT.LE.(Tx(maxNtx)+10.0) ) THEN             ! find nearest value
         diff = 1000.
         DO kx = 1 , Ntx
           diff = MIN( diff, ABS(TT-Tx(kx)) )
         ENDDO
         DO jx = 1 , Ntx
           IF ( diff .EQ. ABS(TT-Tx(jx)) ) kx = jx           ! index of nearest value
         ENDDO   ! end jx
         IF ( kx .LT. 2 ) kx = 2                  
         IF ( kx .GT. Ntx-1 ) kx = Ntx-1                  

c           Assume LOG(QT) vs. 1/T is a straight line over this limited range
         xa = 1.d+00/Tx(kx-1)
         xb = 1.d+00/Tx(kx)
         xc = 1.d+00/Tx(kx+1)
         xx = 1.d+00/TT
         diff = xx - xb
         slope = LOG( QT(i,kx+1)/QT(i,kx-1) )/(xc-xa)
         Qxx = QT(i,kx)*EXP( diff*slope )

c           Assume X(T) is a straight line
         xa = Tx(kx-1)
         xb = Tx(kx)
         xc = Tx(kx+1)
         xx = TT
         diff = xx - xb
         slope = ( Cx(i,kx+1) - Cx(i,kx-1) )/(xc-xa)
         Cxx = Cx(i,kx) + diff*slope
         
         slope = ( Hx(i,kx+1) - Hx(i,kx-1) )/(xc-xa)
         Hxx = Hx(i,kx) + diff*slope
         
         slope = ( Sx(i,kx+1) - Sx(i,kx-1) )/(xc-xa)
         Sxx = Sx(i,kx) + diff*slope
      ELSEIF ( TT .LT. Tx(1) ) THEN
         write(*,*) '***FATAL: For SCTST or BDENS INPUT, user-selected'
         write(*,*) '          temperatures must be >1 K'
         write(3,*) '***FATAL: For SCTST or BDENS INPUT, user-selected'
         write(3,*) '          temperatures must be >1 K'
         STOP
      ELSEIF ( TT .GT. Tx(maxNtx) ) THEN
         write(*,*) '***FATAL: For SCTST or BDENS INPUT, user-selected'
         write(*,*) '          temperatures must be < 3510 K'
         write(3,*) '***FATAL: For SCTST or BDENS INPUT, user-selected'
         write(3,*) '          temperatures must be < 3510 K'
         STOP
      ENDIF
         
      RETURN
         
      END SUBROUTINE qvbint

